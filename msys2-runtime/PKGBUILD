# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.5.1
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0020-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0021-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0022-strace.cc-Don-t-set-MSYS-noglob.patch
        0023-Add-debugging-for-build_argv.patch
        0024-Add-debugging-for-strace-make_command_line.patch
        0025-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0026-path.cc-Ignore-zero-length-exclusions.patch)
sha256sums=('SKIP'
            '3fb7dcd1ec724d5bd35334942b859ac2289a1bf6f2fd2d59a985fdca4dd1eabf'
            '2734865e097baf07779f207cd5259d6e80e8037a7942a7789333c5ec08dcbeb4'
            'a5ecfe028f71f6302bf6a02caa49c4b1a5532de8925d6fcbe2da95e08b43a4ea'
            '8eaf632a7bcad41e57b7f124d2c2bfa8906db81398bf0bb45b0ff5ea70825f05'
            '49e4961da10545dd62a19cf34db5e078aa728d050492fb8f0075c813390e18b0'
            'ef814a9bf5cb3b9df0fad392fa6b21397e7551fd07bd9d88694ea06d0abb49d6'
            'df647ce9e2f7b9225ba5d56bc6ac5084763ad18ce2d35dc0a57dfccea8ac64b8'
            '52f2370c6a546892d49371ad4f17ac49f4892a4d05787449ea009743e8f41eb4'
            '0359ba1318c09a2c90fd7ceb275716117e97baeb1b5c049ba909b6242b4b7513'
            '2e4660599d27b0d03d646e1da914fa3d6a78f2c3dc2fe93de56dc54a9846da94'
            '8c130927882abc172332b68f50c9f5155efc730538f302759d81eb0d3aa3a60f'
            'a375b87aa892711a13d11b4b342b4aa6b50649e944cb6070a28ce6f35957fb01'
            '62936e64f825724c6a63f453f7a0db5012c45c122fd8de1c6c4bf574e9744716'
            '603bbb95081729b048104ef32de05295c22fec8d962bf72c8d204e022f45c287'
            'af35ea0336da3573bbfc7cefdd37a9715e85eafa5e035fe90e4f1c8212547a40'
            '46aeaa5bdd292c82fc08b6a11a561efaa16a02a915a64899fe63f9a720c34a18'
            'afd8454662eb6ea9baa816fc594c3af33f4204d364d148c243c45e556e08613e'
            'dc03861d4614c228afb000e081b46569ce7a4fe7b4082f690b00847e8063da2b'
            'b8da17a374e9fe21f2051582744fb71cdbc7eea009abeeef055ab1ed4d854c83'
            '0bdf0308ddef05a3abdc3a091760f2b8e1055cc1232fd95135beb5ac6063985e'
            '51894036cd0ad1f8deab4761e9876fb92e8b2ce49bceaf3bc4155c7acf2e003f'
            'acd5641aad787dbf9ac31be9d3d7d4ab32cb7b10bb071161abbe6d98df169d36'
            'ddbfcdf68e44404d161fb0cc947abf672c974ef67274f649a26de50fd798c111')
prepare() {
  cd "${srcdir}"/msys2-runtime
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-path.cc-Ignore-zero-length-exclusions.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
